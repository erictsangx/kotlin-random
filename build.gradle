buildscript {
    ext.kotlin_version = '1.1.1'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath group: 'org.junit.platform', name: 'junit-platform-gradle-plugin', version: '1.0.0-M3'
    }
}

apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: 'org.junit.platform.gradle.plugin'

apply plugin: 'maven'
apply plugin: 'signing'

group = 'io.github.erictsangx'
archivesBaseName = "kotlin-random"
version = "0.0.1"


junitPlatform {
    filters {
        engines {
            include 'junit-jupiter'
        }
        includeClassNamePatterns '.*Test', '.*Spec'
    }
}


repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile("org.junit.jupiter:junit-jupiter-api:5.0.0-M3")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.0.0-M3")
}



task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar, javadocJar
}

signing {
    sign configurations.archives
}


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name "${project.group}:${project.archivesBaseName}"
                packaging 'jar'
                description "A very simple lib of Java Random and Kotlin aims to make testing easier, no magic."
                url "https://github.com/erictsangx/kotlin-random"

                scm {
                    connection 'scm:git:git://github.com/erictsangx/kotlin-random.git'
                    developerConnection 'scm:git:ssh://github.com/erictsangx/kotlin-random.git'
                    url 'https://github.com/erictsangx/kotlin-random'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'erictsangx'
                        name 'Eric Tsang'
                        email 'erictsangx@gmail.com'
                    }
                }
            }
        }
    }
}